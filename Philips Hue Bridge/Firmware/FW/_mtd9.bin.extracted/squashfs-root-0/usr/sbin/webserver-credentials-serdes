#!/usr/bin/awk -f

function formatSection(section, value) {
	WIDTH=64
	print "-----BEGIN " section "-----"
	while(value) {
		print substr(value, 1, WIDTH)
		value = substr(value, WIDTH+1)
	}
	print "-----END " section "-----"
}

function fatal(message) {
	print "error: " message > "/dev/stderr"
	exit 1
}

BEGIN {
	for(i=1; i < ARGC; i++) {
		if(ARGV[i] == "encode") {
			encode=1
			delete ARGV[i]
		} else if(ARGV[i] == "decode-key") {
			decode_key=1
			delete ARGV[i]
		} else if(ARGV[i] == "decode-cert") {
			decode_cert=1
			delete ARGV[i]
		}
	}
	FS=":"
}

/([A-Za-z0-9\+\/=]+):([A-Za-z0-9\+\/=]+):([A-Za-z0-9\+\/=]+)/ {
	ec_parameters=$1
	ec_private_key=$2
	ec_certificate=$3
	if(decode_key) {
		formatSection("EC PARAMETERS", ec_parameters)
		formatSection("EC PRIVATE KEY", ec_private_key)
	}
	if(decode_cert) {
		formatSection("CERTIFICATE", ec_certificate)
	}
	ec_parameters=""
	ec_private_key=""
	ec_certificate=""
}

/-----BEGIN EC PARAMETERS-----/,/-----END EC PARAMETERS-----/ {
	if($0 ~ /-----/) {
		next
	}
	ec_parameters=ec_parameters $0
}

/-----BEGIN EC PRIVATE KEY-----/,/-----END EC PRIVATE KEY-----/ {
	if($0 ~ /-----/) {
		next
	}
	ec_private_key=ec_private_key $0
}

/-----BEGIN CERTIFICATE-----/,/-----END CERTIFICATE-----/ {
	if($0 ~ /-----/) {
		next
	}
	ec_certificate=ec_certificate $0
}

END {
	if(encode) {
		if(!ec_parameters) {
			fatal("no EC PARAMETERS section in input")
		}
		if(!ec_private_key) {
			fatal("no EC PRIVATE KEY section in input")
		}
		if(!ec_certificate) {
			fatal("no EC CERTIFICATE section in input")
		}
		print ec_parameters ":" ec_private_key ":" ec_certificate
	}
}

